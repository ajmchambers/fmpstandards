/**
 * ======================================
 * UUIDGetTimestamp ( theID )
 *
 * PURPOSE:
 *		Extracts the creation timestamp from a numeric UUID created by this
 *		family of UUID functions. The returned value can be converted to a more
 *		conventional format with the GetAsTimestamp function.
 *
 * RETURNS:
 *		A number representing the number of seconds since 0001-01-01 00:00:00
 *		when the UUID passed as a parameter to the function was created. Returns
 *		Null ("") if the UUID is in a format that does not contain a retrievable
 *		timestamp.
 *
 * PARAMETERS:
 *		theID: The UUID to extract a timestamp from.
 *
 * DEPENDENCIES: none
 *
 * RELEASE: 2011-04-06
 *
 * REFERENCES:
 *		Key values Best Practice: http://filemakerstandards.org/pages/viewpage.action?pageId=557138
 *		Ray Cologon's uID functions: http://www.nightwing.com.au/FileMaker/demos9/demo910.html
 * ======================================
 */
Let( [
	~idAsNumber = GetAsNumber ( theID );
	~length = Length ( ~idAsNumber );
	~version = Left ( ~idAsNumber ; ~length - 40 )
];
	If ( ~version < 3;
		GetAsTimestamp ( GetAsNumber (
			Middle ( ~idAsNumber ; ~length - 38 ; 12 ) //seconds
			& "."
			& Middle ( ~idAsNumber ; ~length - 26 ; 7 ) //sub-seconds
		) )

		/* Else, UUID does not have a timestamp; return Null */
	)
)